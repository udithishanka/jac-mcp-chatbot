import base64;
import sys;
import os;
import mcp_client;
import from mtllm.llm {Model}
import from rag {RagEngine}

glob rag_engine:RagEngine = RagEngine();
glob llm = Model(model_name='gpt-4o');

glob MCP_SERVER_URL: str = os.getenv('MCP_SERVER_URL', 'http://localhost:8899/mcp');

def call_mcp_tool(name: str, arguments: dict) -> str {
    return mcp_client.call_mcp_tool(name=name, arguments=arguments);
}

node Session {
    has id: str;
    has chat_history: list[dict];
    has status: int = 1;

    """Generate a response using uploaded documents and web search. Tool names are 'search_docs', 'search_web'."""
    def respond(message:str, chat_history:list[dict]) -> str
        by llm(
            method="ReAct",
            tools=([call_mcp_tool]),
            max_react_iterations=6
        );
}


walker interact {
    has message: str;
    has session_id: str;

    can init_session with `root entry {
         visit [-->](`?Session)(?id == self.session_id) else {
            session_node = here ++> Session(id=self.session_id, chat_history=[], status=1);
            print("Session Node Created");
            visit session_node;
        }
    }

    can chat with Session entry {
        here.chat_history.append({"role": "user", "content": self.message});
        response = here.respond(
            message=self.message,
            chat_history=here.chat_history,
        );

        here.chat_history.append({"role": "assistant", "content": response});

        report {"response": response};
    }
}

walker upload_pdf {
    has file_name: str;
    has file_data: str;

    can save_doc with `root entry {
        if not os.path.exists(rag_engine.file_path) {
            os.makedirs(rag_engine.file_path);
        }
        file_path = os.path.join(rag_engine.file_path, self.file_name);
        data = base64.b64decode(self.file_data.encode('utf-8'));
        with open(file_path, 'wb') as f {
            f.write(data);
        }
        rag_engine.add_file(file_path);
        report {"status": "uploaded"};
    }
}




